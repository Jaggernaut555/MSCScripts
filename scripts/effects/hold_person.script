//Hold person by Thothie
//- I think effects/effect_xxx is kinda redundant, so I've broken than convention

//Any npc
//Param1 - Hold Duration
//Param2 - Who to blame
//Param3 - Dmg, if any
//Param4 - Skill to give dmg to
//Param5 - most hp it can affect (def: 1500)

// . effectme effects/hold_person 10 GAME_MASTER 0.5 spellcasting.cold 1500

{ [server]
	const EFFECT_ID effect_hold
	const EFFECT_FLAGS nostack
	const EFFECT_SCRIPT $currentscript //Must be copied, as is, at the top script!

	const SOUND_FREEZE magic/freeze.wav

	const DOT_TYPE lightning_effect
	
	const DOT_IM_AFFECTED "You are held in a magic field!" //Player recieves affect and is afflicted
	
	const DOT_IM_RESIST "You resist being held in a magic field." //Player recieves the affect, but resists / immune it.
	const DOT_HE_IMMUNE "is immune to lightning magic!" //Target recieves the affect, but resists / immune it
}

{ game_activate //This is above the include intentionally

	setvard MAX_HP PARAM5 //Sets the max_hp var before dot_check_canapply is called from the include below
	if ( !PARAM5 ) setvard MAX_HP 1500
}

#include effects/base_dot	allowduplicate

{ dot_check_canapply //<duration> <afflicter> <dot> <skill> <hpmax> <fx:0=elect,1=stone>

	if ( $get(ent_me,width) > 48 )
	{
		dplayermessage DOT_ATTACKER $get(ent_me,name) too large for force cage.
		
		setvard DOT_RESISTED 1
		removescript
		exitevent
	}
	
	if ( $get(ent_me,hp) > MAX_HP )
	{
		playermessage DOT_ATTACKER $get(ent_me,name) is too strong for force cage.
		
		setvard DOT_RESISTED 1
		removescript
		exitevent
	}
}

{ dot_start

	playermessage ent_me 
	playsound 0 10 SOUND_FREEZE

	setvard game.effect.movespeed 0
	setvard game.effect.canjump 0
	setvard game.effect.canduck 0
	setvard game.effect.anim.framerate 0

	setvelocity ent_me $vec(0,0,0)

	scriptflags ent_me add hold_person nopush 1 EFFECT_DURATION none
	callexternal $get(ent_me,id) ext_set_frozen EFFECT_DURATION
	
	infomsg all $get(ent_me,height) $get(ent_me,width)
	clientevent new all effects/hold_person_cl $get(ent_me,index) EFFECT_DURATION
	setvard CL_FX game.script.last_sent_id
}

{ effect_die

	if !DOT_RESISTED
	playermessage ent_me "The magic field dissipates."
	
	if ( CL_FX ) clientevent update all CL_FX end_fx

	callexternal $get(ent_me,id) ext_set_unfrozen

	playsound 0 10 magic/energy1_loud.wav
}
