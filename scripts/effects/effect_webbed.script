//Webbed npcs are slowed. Continuous webbing cocoons the target.
//Stacked webbed effects increase the intensity of the web.

//Large enemies take more web to cocoon
//Small enemies dont take as much web to cocoon

//. effectmestack effects/effect_webbed 10 GAME_MASTER

#scope server

{
	const EFFECT_ID webbed
	const EFFECT_SCRIPT $currentscript //Must be copied, as is, at the top script!
	
	const PLAYER_WEB_TILL_COCOON 10 //Number of webs it takes to cocoon a player.
	//WEB_PER_SIZE will not be used if effect is on a player!
	
	const WEB_PER_CUBE 16 //Volume of monster / x = amt of web it takes to cocoon
	
	setvard WEBS_ATTACHED 0
	setvard WEBS_TILL_COCOON 0 //Set in game_activate
	
	setvar WEB_DECAY_TIME 0 //Largest decay time will override any subsequent smaller ones
}

#include effects/base_effect	allowduplicate

{ game_activate //PARAM1 = Single web decay time; PARAM2 = Webber

	local L_DECAY_TIME PARAM1 //Largest decay time will override any smaller ones

	if ( $get_scriptflag(ent_me,spider_resist,type_exists) ) //Get immunity
	{
		playermessage PARAM2 $get(ent_me,name) "is immune to webs!"
		
		setvard EFFECT_DUPLICATED 1 //Helps prevent silliness
		removescript
		exitevent
	}
	
	if ( !EFFECT_DUPLICATED ) //First instance of web
	{
		//set up fx
		//clientevent new all effects/sfx_webbed $get(ent_me,index) WEBS_ATTACHED
		
		callevent set_cocoon_amt //Get amt of webs until cocoon
		
		setvard game.effect.canjump 0 //No jump allowed while webbed
		setvard game.effect.canrun 0 //No running, the movespeed change will make run ineffective anyways
		
		if ( $get(ent_me,isplayer) )
		{
			playermessage ent_me "You have been webbed!"
			//clientevent update ent_me const.localplayer.scriptID web_effect $get(ent_me,index) 1
		}
	}

	infomsg all EFFECT_DUPLICATED "web"
	callexternal $get(ent_me,id) ext_webbed L_DECAY_TIME //Apply a web
}

{ set_cocoon_amt //sets WEBS_TILL_COCOON by either player, or volume

	if ( $get(ent_me,isplayer) )
	{
		setvard WEBS_TILL_COCOON PLAYER_WEB_TILL_COCOON
	}
	else
	{
		local L_MY_VOLUME $get(ent_me,height)
		multiply L_MY_VOLUME $get(ent_me,width)
		multiply L_MY_VOLUME $get(ent_me,width)
		
		divide L_MY_VOLUME WEB_PER_CUBE
		
		setvard WEBS_TILL_COCOON $int(L_MY_VOLUME)
	}
}

{ ext_webbed

	if !EFFECT_DUPLICATED
	
	infomsg all "ext_webbed" "ext_webbed" 
	if ( PARAM1 > WEB_DECAY_TIME ) //Keep longest web decay time applied
	{
		setvard WEB_DECAY_TIME PARAM1
	}
	
	add WEBS_ATTACHED 1 //Attach a web
	
	if ( WEBS_ATTACHED < WEBS_TILL_COCOON )
	{
		//Override original effect end time
		setvard EFFECT_STARTED game.time
		setvard EFFECT_DURATION $math(multiply,WEB_DECAY_TIME,WEBS_ATTACHED)
		
		//Set next decay timer
		setvard NEXT_DECAY $math(add,game.time,WEB_DECAY_TIME)
		callevent NEXT_DECAY web_decay
		
		//Update slowness and web fx intensity
		callevent web_update
		
		playsound 0 10 bullchicken/bc_acid2.wav
	}
	else
	{
		createnpc monsters/summon/cocoon COCOON_ORG $get(ent_me,id) COCOON_STRENGTH COC_DURATION COC_DOT WEBBER_ID
	}
}

{ web_decay

	if ( game.time >= NEXT_DECAY )
	{
		playermessage ent_me "Some of the webs loosen."
		subtract WEBS_ATTACHED 1
		
		//Set next decay timer
		setvard NEXT_DECAY $math(add,game.time,WEB_DECAY_TIME)
		callevent NEXT_DECAY web_decay
		
		callevent web_update
	}
}

{ web_update

	local L_SPEED 100 
	subtract L_SPEED $math(multiply,5,WEBS_ATTACHED)

	setvard game.effect.movespeed L_SPEED
	setvard game.effect.anim.framerate $math(divide,L_SPEED,100)
}

{ effect_die

	if !EFFECT_DUPLICATED
	if ( $get(ent_me,isplayer) )
	{
		//clientevent update ent_me const.localplayer.scriptID fx_web_set_webs 0
		if ( !DID_COCOON ) playermessage ent_me "The webs have fallen away."
	}
}

{ ext_set_cocooned

	if ( PARAM1 )
	{
		setvard I_R_FROZEN 1
		setvard IN_ICECAGE 1
		setvard CANT_TURN 1
		setvard NO_STUCK_CHECKS 1
		setvard NPC_ORIG_ROAM $get(ent_me,roam)
		roam 0
	}
	else
	{
		setvard I_R_FROZEN 0
		setvard IN_ICECAGE 0
		setvard CANT_TURN 0
		setvard NO_STUCK_CHECKS 0
		roam NPC_ORIG_ROAM
	}
}

{ game_duplicated //Gets called before game_activate

	if ( !EFFECT_DUPLICATED )
	{
		setvard EFFECT_DUPLICATED 1
		removescript
	}
}
