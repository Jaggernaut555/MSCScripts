//holds person/npc in place with a beam cage.

// . effectmestack effects/force_cage 15 ent_me

#scope server

{
	const EFFECT_ID "debuff_hold" //shares
	const EFFECT_SCRIPT $currentscript //Must be copied, as is, at the top script!
	const EFFECT_FLAGS nostack

	const SOUND_FREEZE magic/bolt_start.wav
}

#include effects/base_debuff_diminishing	allowduplicate

{ game_activate //<duration> <owner>

	setvard CAGE_OWNER PARAM2

	if ( $get(ent_me,width) > 48 ) local L_TOO_BIG 1
	if ( $get(ent_me,height) > 96 ) local L_TOO_BIG 1
	if ( L_TOO_BIG )
	{
		dplayermessage PARAM2 Force Cage: $get(ent_me,name) too large for force cage
		removescript
		exitevent
	}
	
	playsound 0 10 SOUND_FREEZE

	setvard game.effect.movespeed 0
	setvard game.effect.canjump 0
	setvard game.effect.canduck 0
	setvard game.effect.anim.framerate 0.01

	playermessage ent_me "You are held in a magic field!"

	callevent do_cage
	callevent sustain_cage

	local L_HOLDER_POS $get(ent_me,origin)

	setvard EFFECT_HOLD_POS L_HOLDER_POS
	
	callevent do_hold_loop
}

{ do_hold_loop

	setorigin ent_me EFFECT_HOLD_POS
	setvelocity ent_me $vec(0,0,0)
	callevent 0.01 do_hold_loop
}

{ do_cage

	local CAGE_ORG $get(ent_me,origin)
	setvard MY_HALF_HEIGHT $get(ent_me,height)
	local MAX_HEIGHT 144
	if ( MY_HALF_HEIGHT <= MAX_HEIGHT )
	{
		local HEIGHT_RATIO MY_HALF_HEIGHT
		divide HEIGHT_RATIO MAX_HEIGHT
		setvard SPHERE_SCALE $ratio(HEIGHT_RATIO,0.01,2.0)
	}
	else
	{
		setvard SPHERE_SCALE 2.0
	}
	multiply MY_HALF_HEIGHT 0.5

	if ( !$get(ent_me,isplayer) ) vectoradd CAGE_ORG z MY_HALF_HEIGHT
	clientevent new all effects/sfx_beam_cage CAGE_ORG SPHERE_SCALE
	setvard CL_CAGE_ID game.script.last_sent_id
	setvard NEXT_CAGE_CL_UPDATE game.time
	add NEXT_CAGE_CL_UPDATE 10.0
}

{ sustain_cage //Replaces the client effect with the same one after 10 seconds
	callevent 1.0 sustain_cage

	if game.time > NEXT_CAGE_CL_UPDATE
	setvard NEXT_CAGE_CL_UPDATE game.time
	add NEXT_CAGE_CL_UPDATE 10.0
	
	clientevent update all CL_CAGE_ID end_fx
	
	local CAGE_ORG $get(ent_me,origin)
	if ( !$get(ent_me,isplayer) ) vectoradd CAGE_ORG z MY_HALF_HEIGHT
	clientevent new all effects/force_cage_cl CAGE_ORG SPHERE_SCALE
	setvard CL_CAGE_ID game.script.last_sent_id
}

{ effect_die

	playermessage The magic field dissipates.

	clientevent update all CL_CAGE_ID end_fx

	playsound 0 10 magic/energy1_loud.wav
}
